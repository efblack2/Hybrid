#!/usr/bin/gnuplot
# call it as:
# gnuplot -c plotCluster.gnp compiler
#
compiler=ARG1

set terminal pdf enhanced
set output "cluster_".compiler.".pdf"

#load 'style.gnp'

set style line 1 dt 1 ps 0.6 lc rgb "#000080";
set style line 2 dt 4 ps 0.5 lc rgb "#ff4500";
set style line 3 dt 5 ps 0.5 lc rgb "#ffd700";

set key top right nobox
set grid

#set title machine
set title "Four nodes cluster for ".compiler." compiler"
#set label "" at screen 0.5, 0.9 center font "Arial,11"
set label "Hybrid Programing Model" at graph 0.5, 1.035 center font "Arial,11"

#set format y "%.2f"
set xlabel "Total number of Processors/Threads"
set ylabel "Time [S]"

set yrange [0:]
set style data histogram
set style histogram cluster gap 1
set style fill solid
set boxwidth 0.90
set xtics format ""
set grid ytics

plot  'cluster/'.compiler.'/laplace.txt' using 6:xtic(1) title "Pure MPI"   ls 1,\
      ''                                 using 18        title "MPI+MPI"    ls 2,\
      ''                                 using 12        title "MPI+OpenMP" ls 3
     


