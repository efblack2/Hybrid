#!/usr/bin/gnuplot
# call it as:
# gnuplot -c plot.gnp stout
#
machine=ARG1

set terminal pdf enhanced
set output machine."Ratio.pdf"

load 'style.gnp'

set key bottom right nobox
set grid

set format y "%.2f"
set xlabel "Number of Processors/Threads"
set ylabel "Time [S]"
     
set title machine
#set label "" at screen 0.5, 0.9 center font "Arial,11"
set label "Hybrid Programing Model" at graph 0.5, 1.035 center font "Arial,11"

plot [0:] [0:] \
        machine.'/gnu/laplace.txt'    using 1:($12/$6) title "MPI to OpenMP Ratio: Gnu"   with linespoints ls 2,\
        machine.'/intel/laplace.txt'  using 1:($12/$6) title "MPI to OpenMP Ratio: Intel" with linespoints ls 4,\
        machine.'/pgi/laplace.txt'    using 1:($12/$6) title "MPI to OpenMP Ratio: Pgi"   with linespoints ls 6,\
        machine.'/gnu/laplace2.txt'   using 1:($12/$6) title "MPI to MPI\_{sm} Ratio: Gnu"   with linespoints ls 7,\
        machine.'/intel/laplace2.txt' using 1:($12/$6) title "MPI to MPI\_{sm} Ratio: Intel" with linespoints ls 8,\
        machine.'/pgi/laplace2.txt'   using 1:($12/$6) title "MPI to MPI\_{sm} Ratio: Pgi"   with linespoints ls 9
     
