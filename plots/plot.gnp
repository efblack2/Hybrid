#!/usr/bin/gnuplot
# call it as:
# gnuplot -c plot.gnp stout
#
machine=ARG1

set terminal pdf enhanced
set output machine.".pdf"
set style line 1 ps 0.5 lc rgb "#ff0000";
set style line 2 ps 0.5 lc rgb "#ff4f00";

set style line 3 ps 0.4 lc rgb "#0000ff";
set style line 4 ps 0.4 lc rgb "#0099ff";

set style line 5 ps 0.4 lc rgb "#00ff00";
set style line 6 ps 0.4 lc rgb "#00ff99";

set key top right box
set grid

set xlabel "Number of Processors/Threads"
set ylabel "Time [S]"
     
set title machine
#set label "" at screen 0.5, 0.9 center font "Arial,11"
set label "Pure Share Memory" at graph 0.5, 1.035 center font "Arial,11"

plot machine.'/gnu/laplace_MPI+MPI.txt'      using 1:6 title "Gnu MPI_{sm}"   with linespoints ls 1,\
     machine.'/gnu/laplace_MPI+openMP.txt'   using 1:6 title "Gnu openMP"     with linespoints ls 2,\
     machine.'/intel/laplace_MPI+MPI.txt'    using 1:6 title "Intel MPI_{sm}" with linespoints ls 3,\
     machine.'/intel/laplace_MPI+openMP.txt' using 1:6 title "Intel openMP"   with linespoints ls 4,\
     machine.'/pgi/laplace_MPI+MPI.txt'      using 1:6 title "Pgi MPI_{sm}"   with linespoints ls 5,\
     machine.'/pgi/laplace_MPI+openMP.txt'   using 1:6 title "Pgi openMP"     with linespoints ls 6 
     
